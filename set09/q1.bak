#lang racket
(require rackunit)
(require "extras.rkt")
(require 2htdp/universe)   
(require 2htdp/image)

(check-location "09" "q1.rkt")

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;; HELPER FUNCTIONS :                                                     ;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;


;;; CONSTANTS

(define CANVAS-WIDTH 500)
(define CANVAS-HEIGHT 600)

(define EMPTY-CANVAS (empty-scene CANVAS-WIDTH CANVAS-HEIGHT))

;; some arbitrary choices
(define INITIAL-X (/ CANVAS-WIDTH 2))  
(define INITIAL-Y (/ CANVAS-HEIGHT 2))
(define SIDE 60)
(define RECT (rectangle SIDE SIDE  "solid" "yellow"))


(define MIN-RADIUS 5)
(define MAX-RADIUS 20)

(define OFFSET 1)

(define NEW-THROBBER-EVENT "t")
(define NEW-CLOCK-EVENT "c")

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

;; Data Definitions

;; A Time is a NonNegative Integer

;; A Widget is an object whose class implements the Widget<%>
;; interface. 

;; A World is an object whose class implements the World<%>
;; interface. 

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

;; INTERFACES

;; big-bang will communicate with the world through the World<%>
;; interface. 

(define World<%>
  (interface ()

    ; -> World
    ; GIVEN: no arguments
    ; RETURNS: the state of the world at the next tick
    after-tick          

    ; Integer Integer MouseEvent-> World
    ; GIVEN: a location
    ; RETURNS: the state of the world that should follow the
    ; given mouse event at the given location.
    after-mouse-event

    ; KeyEvent : KeyEvent -> Widget
    ; GIVEN: a key event
    ; RETURNS: the state of the world that should follow the
    ; given key event
    after-key-event     

    ; -> Scene
    ; GIVEN: a scene
    ; RETURNS: a scene that depicts this World
    to-scene
    ))


;; Every object that lives in the world must implement the Widget<%>
;; interface.

(define Widget<%>
  (interface ()

    ; -> Widget
    ; GIVEN: no arguments
    ; RETURNS: the state of this object that should follow at time t+1.
    after-tick          

    ; Integer Integer -> Widget
    ; GIVEN: a location
    ; RETURNS: the state of this object that should follow the
    ; specified mouse event at the given location.
    after-button-down
    after-button-up
    after-drag

    ; KeyEvent : KeyEvent -> Widget
    ; GIVEN: a key event and a time
    ; RETURNS: the state of this object that should follow the
    ; given key event
    after-key-event     

    ; Scene -> Scene
    ; GIVEN: a scene
    ; RETURNS: a scene like the given one, but with this object
    ; painted on it.
    add-to-scene
    ))


;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;


;; initial-world : -> World
;; RETURNS: a world with a throbber  and no bombs
(define (initial-world)
  (make-world
    empty
    0))

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;


; run : PosReal -> World
; GIVEN: a frame rate, in secs/tick
; EFFECT: runs an initial world at the given frame rate
; RETURNS: the final state of the world
(define (run rate)
  (big-bang (initial-world)
    (on-tick
      (lambda (w) (send w after-tick))
      rate)
    (on-draw
      (lambda (w) (send w to-scene)))
    (on-key
      (lambda (w kev)
        (send w after-key-event kev)))
    (on-mouse
      (lambda (w mx my mev)
        (send w after-mouse-event mx my mev)))))

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

;; The World% class

;; We will have only one class that implements World<%>: World%

;; Constructor template for World%:
;; (new World% [objs ListOfWidget][t Time])
;; Interpretation: An object of class World% takes signals from
;; big-bang and distributes them to its objects as appropriate.


;; make-world : ListOfWidget Time -> World
;; GIVEN: a list of widgets and a time
;; RETURNS: an object of class World% containing the given list of
;; widgets and time.
(define (make-world objs t)
  (new World% [objs objs][t t]))

(define World%
  (class* object% (World<%>)

    (init-field objs) ;  ListOfWidget
    (init-field t)    ;  Time

    (super-new)

    ;; after-tick : -> World
    ;; Use HOFC map on the Widget's in this World

    (define/public (after-tick)
      (make-world
        (map
          (lambda (obj) (send obj after-tick))
          objs)
        (+ 1 t)))

    ;; to-scene : -> Scene
    ;; Use HOFC foldr on the Widgets in this World

    (define/public (to-scene)
      (foldr
        (lambda (obj scene)
          (send obj add-to-scene scene))
        EMPTY-CANVAS
        objs))


    ;; after-key-event : KeyEvent -> World
    ;; STRATEGY: Cases on kev
    ;; "b" and "h" create new bomb and new throbber ;
    ;; other keystrokes are passed on to the objects in the world.

    (define/public (after-key-event kev)
      (cond
        [(key=? kev NEW-THROBBER-EVENT)
         (make-world
           (cons (new-throbber) objs)
           t)]
        [(key=? kev NEW-CLOCK-EVENT)
         (make-world
           (cons (new-clock) objs)
           t)]
        [else
          (make-world
            (map
              (lambda (obj) (send obj after-key-event kev))
              objs)
            t)]))

    ;; world-after-mouse-event : Nat Nat MouseEvent -> World
    ;; STRATEGY: Cases on mev
    (define/public (after-mouse-event mx my mev)
      (cond
        [(mouse=? mev "button-down")
         (world-after-button-down mx my)]
        [(mouse=? mev "drag")
         (world-after-drag mx my)]
        [(mouse=? mev "button-up")
         (world-after-button-up mx my)]
        [else this]))

    ;; the next few functions are local functions, not in the interface.

    (define (world-after-button-down mx my)
      (make-world
        (map
          (lambda (obj) (send obj after-button-down mx my))
          objs)
        t))
    
     
    (define (world-after-button-up mx my)
      (make-world
        (map
          (lambda (obj) (send obj after-button-up mx my))
          objs)
        t))

    (define (world-after-drag mx my)
      (make-world
        (map
          (lambda (obj) (send obj after-drag mx my))
          objs)
        t))

    ))


;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

;;; We have two classes of Widget<%>s:  throbber s and Bombs

;; throbber s start near the bottom of the screen and rise slowly.
;; They are selectable and draggable.

;; Constructor template for throbber%:
;; (new throbber% [x Integer][y Integer]
;;            [selected? Boolean][mx Integer][my Integer])
;; the last 3 arguments are optional
;; Interpretation: An object of class throbber% represents a throbber .


;; unchanged from 09-2-1.
(define throbber%
  (class* object% (Widget<%>)

    ;; the init-fields are the values that may vary from one throbber  to
    ;; the next.

    ; the x and y position of the center of the throbber  and radius
    (init-field x y flag r)   

    ; is the throbber  selected? Default is false.
    (init-field [selected? false]) 

    ;; if the throbber  is selected, the position of
    ;; the last button-down event inside the throbber , relative to the
    ;; throbber 's center.  Else any value.
    (init-field [saved-mx 0] [saved-my 0])

    ;; private data for objects of this class.
    ;; these can depend on the init-fields.
  

    ; image for displaying the throbber 
    (field [throbber-IMG-UNSELECTED (circle r "solid" "green")])
    ; image for displaying the throbber 
    (field [throbber-IMG-SELECTED (circle r "outline" "green")])   
       
    (super-new)



    (define (check-inc-or-dec? r f)
      (cond
        [(equal? r ( - MAX-RADIUS OFFSET)) false]
        [(equal? r (+ MIN-RADIUS OFFSET)) true]
        [else f]))

    (define (radius-changer r f)
      (cond
        [(equal? true f) (+ r OFFSET)]
        [else (- r OFFSET)]))



    
      
    
    
    ;; after-tick : Time -> Widget
    ;; RETURNS: A throbber  like this one, but as it should be after a tick
    ;; a selected throbber  doesn't move.
    ;; STRATEGY: Cases on selected?
    (define/public (after-tick)
      (if selected?
        this
        (new throbber%
          [x x]
          [y y]
          [flag (check-inc-or-dec? r flag)]
          [r (radius-changer r flag)]
          [selected? selected?]
          [saved-mx saved-mx]
          [saved-my saved-my])))
    
    ;; after-key-event : KeyEvent -> Widget
    ;; RETURNS: A world like this one, but as it should be after the
    ;; given key event.
    ;; DETAILS: a throbber  ignores key events
    (define/public (after-key-event kev)
      this)      
    
    ; after-button-down : Integer Integer -> Widget
    ; GIVEN: the location of a button-down event
    ; STRATEGY: Cases on whether the event is in the throbber 
    (define/public (after-button-down mx my)
      (if (in-throbber? mx my)
        (new throbber%
          [x x][y y]
          [r r]
          [flag flag]
          [selected? true]
          [saved-mx (- mx x)]
          [saved-my (- my y)])
        this))

    ; after-button-up : Integer Integer -> Widget
    ; GIVEN: the location of a button-up event
    ; STRATEGY: Cases on whether the event is in the throbber .
    ; If the throbber  is selected, then unselect it.
    (define/public (after-button-up mx my)
      (if (in-throbber? mx my)
        (new throbber%
          [x x][y y]
          [r r] [flag flag]
          [selected? false]
          [saved-mx saved-mx]
          [saved-my saved-my])
        this))   

    ; after-drag : Integer Integer -> Widget
    ; GIVEN: the location of a drag event
    ; STRATEGY: Cases on whether the throbber  is selected.
    ; If it is selected, move it so that the vector from the center to
    ; the drag event is equal to (mx, my)
    (define/public (after-drag mx my)
      (if selected?
        (new throbber%
          [x (- mx saved-mx)]
          [y (- my saved-my)]
          [r r] [flag flag]
          [selected? true]
          [saved-mx saved-mx]
          [saved-my saved-my])
        this))   


    ;; to-scene : Scene -> Scene
    ;; RETURNS: a scene like the given one, but with this throbber  painted
    ;; on it.
    (define/public (add-to-scene scene)
      (cond
        [(equal? selected? false)
         (place-image throbber-IMG-UNSELECTED x y scene)]
        [else (place-image throbber-IMG-SELECTED x y scene)]))
    
    ;; in-throbber? : Integer Integer -> Boolean
    ;; GIVEN: a location on the canvas
    ;; RETURNS: true iff the location is inside this throbber .
    (define (in-throbber? other-x other-y)
      (<= (+ (sqr (- x other-x)) (sqr (- y other-y)))
          (sqr r)))

    ;; test methods, to probe the throbber  state.  Note that we don't have
    ;; a probe for radius.
    ;; -> Int
    (define/public (for-test:x) x)
    ;; -> Int
    (define/public (for-test:y) y)
    ;; -> Boolean
    (define/public (for-test:selected?) selected?)

    ;; -> (list Int Int Boolean)
    (define/public (for-test:throbber-state) (list x y selected?))
    
    ))

;; make-throbber: -> Widget
;; GIVEN: no arguments
;; RETURNS: a new object of class throbber% near the bottom of the screen.

;; NOTE: the contract says Widget, because our contracts are ALWAYS in
;; terms of interfaces (remember, a Widget is an object that
;; implements Widget<%>).  The purpose statement gives the additional
;; information that the Widget returned by make-throbber happens to be an
;; object of class throbber%.

(define (new-throbber)
  (new throbber% [x INITIAL-X][y INITIAL-Y] [flag true] [r MIN-RADIUS]))

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

;;; We have two classes of Widget<%>s:  clock s and Bombs

;; clock s start near the bottom of the screen and rise slowly.
;; They are selectable and draggable.

;; Constructor template for clock%:
;; (new clock% [x Integer][y Integer]
;;            [selected? Boolean][mx Integer][my Integer])
;; the last 3 arguments are optional
;; Interpretation: An object of class clock% represents a clock .


;; unchanged from 09-2-1.
(define clock%
  (class* object% (Widget<%>)

    ;; the init-fields are the values that may vary from one clock  to
    ;; the next.

    ; the x and y position of the center of the clock  and time of clock
    (init-field x y t)   

    ; is the clock  selected? Default is false.
    (init-field [selected? false]) 

    ;; if the clock  is selected, the position of
    ;; the last button-down event inside the clock , relative to the
    ;; clock 's center.  Else any value.
    (init-field [saved-mx 0] [saved-my 0])

    ;; private data for objects of this class.
    ;; these can depend on the init-fields.
  
       
    (super-new)


    ;; get-text : NonNegInt -> Scene
    ;; GIVEN : time i.e number of ticks till start.
    ;; RETURNS: a scene with time as an image.
    ;; EXAMPLE: see test case
    ;;       
    ;; STRATEGY: combine simpler functions
    
    (define (get-text time-value)
      (text (number->string time-value) 15 "red"))
    
    
    ;; overlay-clock-text : Scene -> Scene
    ;; GIVEN : an image of a rectangle
    ;; RETURNS: a scene with overlay of time over the rectangle.
    ;; EXAMPLE: see test case
    ;;       
    ;; STRATEGY: combine simpler functions
    
    (define (overlay-clock-text time-value)
      (overlay (get-text time-value) RECT))
    
      
    
    
    ;; after-tick : Time -> Widget
    ;; RETURNS: A clock  like this one, but as it should be after a tick
    ;; a selected clock  doesn't move.
    ;; STRATEGY: Cases on selected?
    (define/public (after-tick)
        (new clock%
          [x x]
          [y y]
          [t (+ 1 t)]
          [selected? selected?]
          [saved-mx saved-mx]
          [saved-my saved-my]))
    
    ;; after-key-event : KeyEvent -> Widget
    ;; RETURNS: A world like this one, but as it should be after the
    ;; given key event.
    ;; DETAILS: a clock  ignores key events
    (define/public (after-key-event kev)
      this)      
    
    ; after-button-down : Integer Integer -> Widget
    ; GIVEN: the location of a button-down event
    ; STRATEGY: Cases on whether the event is in the clock 
    (define/public (after-button-down mx my)
      (if (in-clock? mx my)
        (new clock%
          [x x][y y]
          [t t]
          [selected? true]
          [saved-mx (- mx x)]
          [saved-my (- my y)])
        this))

    ; after-button-up : Integer Integer -> Widget
    ; GIVEN: the location of a button-up event
    ; STRATEGY: Cases on whether the event is in the clock .
    ; If the clock  is selected, then unselect it.
    (define/public (after-button-up mx my)
      (if (in-clock? mx my)
        (new clock%
          [x x][y y]
          [t t]
          [selected? false]
          [saved-mx saved-mx]
          [saved-my saved-my])
        this))   

    ; after-drag : Integer Integer -> Widget
    ; GIVEN: the location of a drag event
    ; STRATEGY: Cases on whether the clock  is selected.
    ; If it is selected, move it so that the vector from the center to
    ; the drag event is equal to (mx, my)
    (define/public (after-drag mx my)
      (if selected?
        (new clock%
          [x (- mx saved-mx)]
          [y (- my saved-my)]
          [t t]
          [selected? true]
          [saved-mx saved-mx]
          [saved-my saved-my])
        this))   


    ;; to-scene : Scene -> Scene
    ;; RETURNS: a scene like the given one, but with this clock  painted
    ;; on it.
    (define/public (add-to-scene scene)
      (place-image (overlay-clock-text t) x y scene))
    
    ;; in-clock? : Integer Integer -> Boolean
    ;; GIVEN: a location on the canvas
    ;; RETURNS: true iff the location is inside this clock .
    (define (in-clock? other-x other-y)
      (<= (+ (sqr (- x other-x)) (sqr (- y other-y)))
          (sqr SIDE)))

    ;; test methods, to probe the clock  state.  Note that we don't have
    ;; a probe for radius.
    ;; -> Int
    (define/public (for-test:x) x)
    ;; -> Int
    (define/public (for-test:y) y)
    ;; -> Boolean
    (define/public (for-test:selected?) selected?)

    ;; -> (list Int Int Boolean)
    (define/public (for-test:clock-state) (list x y selected?))
    
    ))

;; make-clock: -> Widget
;; GIVEN: no arguments
;; RETURNS: a new object of class clock% near the bottom of the screen.

;; NOTE: the contract says Widget, because our contracts are ALWAYS in
;; terms of interfaces (remember, a Widget is an object that
;; implements Widget<%>).  The purpose statement gives the additional
;; information that the Widget returned by make-clock happens to be an
;; object of class clock%.

(define (new-clock)
  (new clock% [x INITIAL-X][y INITIAL-Y] [t 0]))

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

